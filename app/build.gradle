plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'org.jetbrains.kotlin.kapt'
    id 'org.jetbrains.kotlin.plugin.serialization' version "$kotlin_version"
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdkVersion project.compileSdk

    defaultConfig {
        applicationId "ua.graviton.isida"
        minSdkVersion project.minSdk
        targetSdkVersion project.targetSdk

        versionCode 1
        versionName "1.0"

        archivesBaseName = "app-v$versionName"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-DEBUG"
            applicationIdSuffix ".debug"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        // Flag to enable support for the new language APIs
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = '1.8'
        useIR = true
    }

    buildFeatures {
        viewBinding true
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlinx:kotlinx-serialization-json:${kotlinx.serialization}"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:${kotlinx.coroutines}"

    // Compose
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.ui:ui-util:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    //implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    implementation "androidx.compose.foundation:foundation:$compose_version"
    implementation "androidx.compose.foundation:foundation-layout:$compose_version"
    implementation "androidx.compose.runtime:runtime:$compose_version"
    implementation "androidx.compose.animation:animation:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    //implementation "androidx.compose.material:material-icons-core:$compose_version"
    implementation "androidx.compose.material:material-icons-extended:$compose_version"

    // Accompanist
    //implementation "com.google.accompanist:accompanist-swiperefresh:${google.accompanist}"
    implementation "com.google.accompanist:accompanist-insets:${google.accompanist}"
    implementation "com.google.accompanist:accompanist-insets-ui:${google.accompanist}"
    //implementation "com.google.accompanist:accompanist-pager:${google.accompanist}"
    //implementation "com.google.accompanist:accompanist-pager-indicators:${google.accompanist}"
    //implementation "com.google.accompanist:accompanist-flowlayout:${google.accompanist}"
    implementation "com.google.accompanist:accompanist-systemuicontroller:${google.accompanist}"
    //implementation "com.google.accompanist:accompanist-placeholder:${google.accompanist}"
    //implementation "com.google.accompanist:accompanist-placeholder-material:${google.accompanist}"

    implementation "androidx.lifecycle:lifecycle-runtime-ktx:${androidx.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:${androidx.lifecycle}"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:${androidx.lifecycle}"
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.4.0'

    implementation 'androidx.navigation:navigation-compose:2.4.0-beta02'
    //implementation 'androidx.paging:paging-compose:1.0.0-alpha14'
    //implementation 'androidx.constraintlayout:constraintlayout-compose:1.0.0-beta02'

    implementation "androidx.room:room-runtime:${androidx.room}"
    kapt "androidx.room:room-compiler:${androidx.room}"
    implementation "androidx.room:room-ktx:${androidx.room}"

    implementation "androidx.core:core-ktx:${androidx.core}"
    implementation 'androidx.core:core-splashscreen:1.0.0-alpha02'
    implementation "androidx.activity:activity-compose:${androidx.activity}"
    implementation "androidx.appcompat:appcompat:${androidx.appcompat}"
    implementation "androidx.preference:preference-ktx:${androidx.preference}"

    implementation "com.google.dagger:hilt-android:${google.hilt}"
    kapt "com.google.dagger:hilt-android-compiler:${google.hilt}"
    implementation 'androidx.hilt:hilt-navigation-compose:1.0.0-alpha03'

    implementation 'com.google.android.gms:play-services-location:18.0.0'

    implementation "com.jakewharton.timber:timber:${versions.timber}"

    coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:$coreDesugaring"
}

static String getVersionSuffix(String extra) {
    def branch = "git branch --show-current".execute().text.trim()
    def commit = "git rev-parse --short HEAD".execute().text.trim()
    def describe = "git describe --tags".execute().text.trim()
    def pattern = ~/(.+)-(\d+)-(.+)/

    if (branch =~ "master") {
        return ""
    } else if (branch =~ "release/(.+)") {
        def matcher = describe =~ pattern
        if (matcher.find()) {
            def (_, tag, count, hash) = matcher[0]
            return "-RC$count${extra ? extra : ""}-$hash"
        } else return "-RC-$commit"
    } else {
        def matcher = describe =~ pattern
        if (matcher.find()) {
            def (_, tag, count, hash) = matcher[0]
            return "-$count${extra ? extra : ""}-$hash"
        }
    }
    return "-$commit"
}

String gitLatestTag() {
    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "tag", "--sort=-committerdate"
        standardOutput = hashStdOut
    }
    return hashStdOut.toString().split("\n").first().trim()
}

String gitNotes(String latestTag) {
    def source = (latestTag) ? "$latestTag..HEAD" : "--max-count=20"

    def hashStdOut = new ByteArrayOutputStream()
    exec {
        commandLine "git", "log", "--pretty=* %s (%an) [%h]", source
        standardOutput = hashStdOut
    }
    return hashStdOut.toString().trim()
}

kapt { correctErrorTypes = true }